apply plugin: 'com.android.application'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "fr.skyle.peoplover"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        // Faster image rendering
        renderscriptTargetApi 21
        renderscriptSupportModeEnabled = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        ndk.abiFilters "arm64-v8a", "armeabi-v7a", "x86", "x86_64"
    }

    buildTypes {
        debug {
            versionNameSuffix '-debug'
            applicationIdSuffix '.debug'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // Export room schema for easy migration
    kapt {
        arguments {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    // Required by Coil and maybe other app
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }
}

task clonePrivateFiles(type: Copy) {
    from('src/main/assets/private/google-services.json')
    into './'
}

// This is needed to avoid duplicate in demo targets
project.afterEvaluate {
    getTasks().getByName("preBuild").finalizedBy(clonePrivateFiles)
}

repositories {
    mavenCentral()
    google()
    maven { url 'https://jitpack.io' }
    maven { url 'https://maven.openium.fr' }

    flatDir {
        dirs 'libs'
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // --- Kotlin version
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    // --- AndroidX
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.material:material:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.0'

    // --- Firebase
    implementation 'com.google.firebase:firebase-analytics:17.2.3'
    implementation 'com.google.firebase:firebase-crashlytics:17.0.0-beta01'

    // --- Koin
    implementation "org.koin:koin-android:2.1.4"
    implementation "org.koin:koin-androidx-viewmodel:2.1.4"

    // --- Timber
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // --- Android tools
    implementation 'fr.openium:kotlin-tools:1.0.14'

    // --- Room
    implementation 'androidx.room:room-runtime:2.2.4'
    kapt 'androidx.room:room-compiler:2.2.4'

    // --- Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.3'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.3'

    // --- KTX
    implementation 'androidx.core:core-ktx:1.2.0'
    implementation 'androidx.room:room-ktx:2.2.4'

    // --- ARCH
    implementation 'androidx.lifecycle:lifecycle-common-java8:2.2.0'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.2.0"

    // --- Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    // --- App Specific ---

    // Coil
    implementation 'io.coil-kt:coil:0.9.5'
}
